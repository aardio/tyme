//ShouXingUtil 寿星天文历算
import time.tyme.base;
import time.tyme.data;
import time.tyme.JulianDay;

namespace time.tyme;

namespace ShouXingUtil {
	JulianDay = ..time.tyme.JulianDay;
	
    PI_2 = 2 * ..math.pi;
    ONE_THIRD = 1.0 / 3;
    SECOND_PER_DAY = 86400;
    SECOND_PER_RAD = 648000 / ..math.pi;

    DT_AT = ..time.tyme.DT_AT;
    XL0 = ..time.tyme.XL0;
    XL1 = ..time.tyme.XL1;
    QI_KB = ..time.tyme.QI_KB;
    QB = ..time.tyme.QB;
    SB = ..time.tyme.SB;

    NUT_B = [
        2.1824, -33.75705, 36e-6, -1720, 920,
        3.5069, 1256.66393, 11e-6, -132, 57,
        1.3375, 16799.4182, -51e-6, -23, 10,
        4.3649, -67.5141, 72e-6, 21, -9,
        0.04, -628.302, 0, -14, 0,
        2.36, 8328.691, 0, 7, 0,
        3.46, 1884.966, 0, -5, 2,
        5.44, 16833.175, 0, -4, 2,
        3.69, 25128.110, 0, -3, 0,
        3.55, 628.362, 0, 2, 0
    ];

    SHUO_KB = [
        1457698.231017, 29.53067166,
        1546082.512234, 29.53085106,
        1640640.735300, 29.53060000,
        1642472.151543, 29.53085439,
        1683430.509300, 29.53086148,
        1752148.041079, 29.53085097,
        1807665.420323, 29.53059851,
        1883618.114100, 29.53060000,
        1907360.704700, 29.53060000,
        1936596.224900, 29.53060000,
        1939135.675300, 29.53060000,
        1947168.00
    ];

    nutationLon2 = function(t){
        var a = -1.742 * t;
        var t2 = t * t;
        var dl = 0;
        for(i=1; #self.NUT_B; 5){
            dl += (self.NUT_B[i + 3] + a) * ..math.sin(self.NUT_B[i] + self.NUT_B[i + 1] * t + self.NUT_B[i + 2] * t2);
            a = 0;
        }
        return dl / 100 / self.SECOND_PER_RAD;
    }

    eLon = function(t, n){
        t /= 10;
        var v = 0;
        var tn = 1;
        var m0 = self.XL0[3] - self.XL0[2];
        for(i=1; 6){
            var n1 = ..math.modf(self.XL0[1 + i]);
            var n2 = ..math.modf(self.XL0[2 + i]);
            var n0 = n2 - n1;
            if(n0 === 0){
                tn *= t;
                continue;
            }
            var m;
            if(n < 0){
                m = n2;
            }
            else {
                m = ..math.modf((3 * n * n0 / m0 + 0.5) + n1);
                if(i != 1){
                    m += 3;
                }
                if(m > n2){
                    m = n2;
                }
            }
            var c = 0;
            for(j=n1; m - 1; 3){
                c += self.XL0[j + 1] * ..math.cos(self.XL0[j + 2] + t * self.XL0[j + 3]);
            }
            v += c * tn;
            tn *= t;
        }
        v /= self.XL0[1];
        var t2 = t * t;
        return v + (-0.0728 - 2.7702 * t - 1.1019 * t2 - 0.0996 * t2 * t) / self.SECOND_PER_RAD;
    }

    mLon = function(t, n){
        var obl = #self.XL1[1];
        var tn = 1;
        var v = 0;
        var t2 = t * t;
        var t3 = t2 * t;
        var t4 = t3 * t;
        var t5 = t4 * t;
        var tx = t - 10;
        v += (3.81034409 + 8399.684730072 * t - 3.319e-05 * t2 + 3.11e-08 * t3 - 2.033e-10 * t4) * self.SECOND_PER_RAD;
        v += 5028.792262 * t + 1.1124406 * t2 + 0.00007699 * t3 - 0.000023479 * t4 - 0.0000000178 * t5;
        if(tx > 0){
            v += -0.866 + 1.43 * tx + 0.054 * tx * tx;
        }
        t2 /= 1e4;
        t3 /= 1e8;
        t4 /= 1e8;

        n *= 6;
        if(n < 0){
            n = obl;
        }
        for(i=1; #self.XL1){
            var f = self.XL1[i];
            var l = #f;
            var m = ..math.modf((n * l / obl + 0.5));
            if(i > 1){
                m += 6;
            }
            if(m >= l){
                m = l;
            }
            var c = 0;
            for(j=1; m; 6){
                c += f[j] * ..math.cos(f[j + 1] + t * f[j + 2] + t2 * f[j + 3] + t3 * f[j + 4] + t4 * f[j + 5]);
            }
            v += c * tn;
            tn *= t;
        }
        return v / self.SECOND_PER_RAD;
    }

    gxcSunLon = function(t){
        var t2 = t * t;
        return -20.49552 * (1 + (0.016708634 - 0.000042037 * t - 0.0000001267 * t2) * ..math.cos(-0.043126 + 628.301955 * t - 0.000002732 * t2)) / self.SECOND_PER_RAD;
    }

    ev = function(t){
        var f = 628.307585 * t;
        return 628.332 + 21 * ..math.sin(1.527 + f) + 0.44 * ..math.sin(1.48 + f * 2) + 0.129 * ..math.sin(5.82 + f) * t + 0.00055 * ..math.sin(4.21 + f) * t * t;
    }

    saLon = function(t, n){
        return self.eLon(t, n) + self.nutationLon2(t) + self.gxcSunLon(t) + ..math.pi;
    }

    dtExt = function(y, jsd){
        var dy = (y - 1820) / 100;
        return -20 + jsd * dy * dy;
    }

    dtCalc = function(y){
        var size = #self.DT_AT;
        var y0 = self.DT_AT[size - 1];
        var t0 = self.DT_AT[size];
        if(y >= y0){
            var jsd = 31;
            if(y > y0 + 100){
                return self.dtExt(y, jsd);
            }
            return self.dtExt(y, jsd) - (self.dtExt(y0, jsd) - t0) * (y0 + 100 - y) / 100;
        }
        var index;
        for(j=1; size; 5){
            index = j;
            if(y < self.DT_AT[j + 5]){
                break;
            }
        }
        var t1 = (y - self.DT_AT[index]) / (self.DT_AT[index + 5] - self.DT_AT[index]) * 10;
        var t2 = t1 * t1;
        var t3 = t2 * t1;
        return self.DT_AT[index + 1] + self.DT_AT[index + 2] * t1 + self.DT_AT[index + 3] * t2 + self.DT_AT[index + 4] * t3;
    }

    dtT = function(t){
        return self.dtCalc(t / 365.2425 + 2000) / self.SECOND_PER_DAY;
    }

    mv = function(t){
        var v = 8399.71 - 914 * ..math.sin(0.7848 + 8328.691425 * t + 0.0001523 * t * t);
        return v - (179 * ..math.sin(2.543 + 15542.7543 * t) + 160 * ..math.sin(0.1874 + 7214.0629 * t) + 62 * ..math.sin(3.14 + 16657.3828 * t) + 34 * ..math.sin(4.827 + 16866.9323 * t) + 22 * ..math.sin(4.9 + 23871.4457 * t) + 12 * ..math.sin(2.59 + 14914.4523 * t) + 7 * ..math.sin(0.23 + 6585.7609 * t) + 5 * ..math.sin(0.9 + 25195.624 * t) + 5 * ..math.sin(2.32 - 7700.3895 * t) + 5 * ..math.sin(3.88 + 8956.9934 * t) + 5 * ..math.sin(0.49 + 7771.3771 * t));
    }

    saLonT = function(w){
        var v = 628.3319653318;
        var t = (w - 1.75347 - ..math.pi) / v;
        v = self.ev(t);
        t += (w - self.saLon(t, 10)) / v;
        return t + (w - self.saLon(t, -1)) / self.ev(t);
    }

    msaLon = function(t, mn, sn){
        return self.mLon(t, mn) + (-3.4E-6) - (self.eLon(t, sn) + self.gxcSunLon(t) + ..math.pi);
    }

    msaLonT = function(w){
        var v = 7771.37714500204;
        var t = (w + 1.08472) / v;
        t += (w - self.msaLon(t, 3, 3)) / v;
        v = self.mv(t) - self.ev(t);
        t += (w - self.msaLon(t, 20, 10)) / v;
        return t + (w - self.msaLon(t, -1, 60)) / v;
    }

    saLonT2 = function(w){
        var v = 628.3319653318;
        var t = (w - 1.75347 - ..math.pi) / v;
        t -= (0.000005297 * t * t + 0.0334166 * ..math.cos(4.669257 + 628.307585 * t) + 0.0002061 * ..math.cos(2.67823 + 628.307585 * t) * t) / v;
        return t + (w - self.eLon(t, 8) - ..math.pi + (20.5 + 17.2 * ..math.sin(2.1824 - 33.75705 * t)) / self.SECOND_PER_RAD) / v;
    }

    msaLonT2 = function(w){
        var v = 7771.37714500204;
        var t = (w + 1.08472) / v;
        var t2 = t * t;
        t -= (-0.00003309 * t2 + 0.10976 * ..math.cos(0.784758 + 8328.6914246 * t + 0.000152292 * t2) + 0.02224 * ..math.cos(0.18740 + 7214.0628654 * t - 0.00021848 * t2) - 0.03342 * ..math.cos(4.669257 + 628.307585 * t)) / v;
        t2 = t * t;
        var l = self.mLon(t, 20) - (4.8950632 + 628.3319653318 * t + 0.000005297 * t2 + 0.0334166 * ..math.cos(4.669257 + 628.307585 * t) + 0.0002061 * ..math.cos(2.67823 + 628.307585 * t) * t + 0.000349 * ..math.cos(4.6261 + 1256.61517 * t) - 20.5 / self.SECOND_PER_RAD);
        v = 7771.38 - 914 * ..math.sin(0.7848 + 8328.691425 * t + 0.0001523 * t2) - 179 * ..math.sin(2.543 + 15542.7543 * t) - 160 * ..math.sin(0.1874 + 7214.0629 * t);
        return t + (w - l) / v;
    }

    qiHigh = function(w){
        var t = self.saLonT2(w) * 36525;
        t = t - self.dtT(t) + self.ONE_THIRD;
        var v = ((t + 0.5) % 1) * self.SECOND_PER_DAY;
        if(v < 1200 || v > self.SECOND_PER_DAY - 1200){
            t = self.saLonT(w) * 36525 - self.dtT(t) + self.ONE_THIRD;
        }
        return t;
    }

    shuoHigh = function(w){
        var t = self.msaLonT2(w) * 36525;
        t = t - self.dtT(t) + self.ONE_THIRD;
        var v = ((t + 0.5) % 1) * self.SECOND_PER_DAY;
        if(v < 1800 || v > self.SECOND_PER_DAY - 1800){
            t = self.msaLonT(w) * 36525 - self.dtT(t) + self.ONE_THIRD;
        }
        return t;
    }

    qiLow = function(w){
        var v = 628.3319653318;
        var t = (w - 4.895062166) / v;
        t -= (53 * t * t + 334116 * ..math.cos(4.67 + 628.307585 * t) + 2061 * ..math.cos(2.678 + 628.3076 * t) * t) / v / 10000000;
        var n = 48950621.66 + 6283319653.318 * t + 53 * t * t + 334166 * ..math.cos(4.669257 + 628.307585 * t) + 3489 * ..math.cos(4.6261 + 1256.61517 * t) + 2060.6 * ..math.cos(2.67823 + 628.307585 * t) * t - 994 - 834 * ..math.sin(2.1824 - 33.75705 * t);
        t -= (n / 10000000 - w) / 628.332 + (32 * (t + 1.8) * (t + 1.8) - 20) / self.SECOND_PER_DAY / 36525;
        return t * 36525 + self.ONE_THIRD;
    }

    shuoLow = function(w){
        var v = 7771.37714500204;
        var t = (w + 1.08472) / v;
        t -= (-0.0000331 * t * t + 0.10976 * ..math.cos(0.785 + 8328.6914 * t) + 0.02224 * ..math.cos(0.187 + 7214.0629 * t) - 0.03342 * ..math.cos(4.669 + 628.3076 * t)) / v + (32 * (t + 1.8) * (t + 1.8) - 20) / self.SECOND_PER_DAY / 36525;
        return t * 36525 + self.ONE_THIRD;
    }

    calcShuo = function(jd){
        var size = #self.SHUO_KB;
        var d = 0;
        var pc = 14;
        jd += JulianDay.J2000;
        var f1 = self.SHUO_KB[1] - pc;
        var f2 = self.SHUO_KB[size] - pc;
        var f3 = 2436935;

        if(jd < f1 || jd >= f3){
            d = ..math.floor(self.shuoHigh(..math.floor((jd + pc - 2451551) / 29.5306) * self.PI_2) + 0.5);
        }
        elseif(jd >= f1 && jd < f2){
            var index;
            for(i=1; size; 2){
                index = i;
                if(jd + pc < self.SHUO_KB[i + 2]){
                    break;
                }
            }
            d = self.SHUO_KB[index] + self.SHUO_KB[index + 1] * ..math.floor((jd + pc - self.SHUO_KB[index]) / self.SHUO_KB[index + 1]);
            d = ..math.floor(d + 0.5);
            if(d === 1683460){
                d++;
            }
            d -= JulianDay.J2000;
        }
        elseif(jd >= f2 && jd < f3){
            var n = ..string.charCodeAt(self.SB, ..math.floor((jd - f2) / 29.5306) + 1);
            d = ..math.floor(self.shuoLow(..math.floor((jd + pc - 2451551) / 29.5306) * self.PI_2) + 0.5);
            if(49 === n){
                d += 1;
            }
            elseif(50 === n){
                d -= 1;
            }
        }
        return d;
    }

    calcQi = function(jd){
        var size = #self.QI_KB;
        var d = 0;
        var pc = 7;
        jd += JulianDay.J2000;
        var f1 = self.QI_KB[1] - pc;
        var f2 = self.QI_KB[size] - pc;
        var f3 = 2436935;

        if(jd < f1 || jd >= f3){
            d = ..math.floor(self.qiHigh(..math.floor((jd + pc - 2451259) / 365.2422 * 24) * ..math.pi / 12) + 0.5);
        }
        elseif(jd >= f1 && jd < f2){
            var index;
            for(i=1; size; 2){
                index = i;
                if(jd + pc < self.QI_KB[i + 2]){
                    break;
                }
            }
            d = self.QI_KB[index] + self.QI_KB[index + 1] * ..math.floor((jd + pc - self.QI_KB[index]) / self.QI_KB[index + 1]);
            d = ..math.floor(d + 0.5);
            if(d === 1683460){
                d++;
            }
            d -= JulianDay.J2000;
        }
        elseif(jd >= f2 && jd < f3){
            d = ..math.floor(self.qiLow(..math.floor((jd + pc - 2451259) / 365.2422 * 24) * ..math.pi / 12) + 0.5);
            var n = ..string.charCodeAt(self.QB, ..math.floor((jd - f2) / 365.2422 * 24) + 1);
            if(49 === n){
                d += 1;
            }
            elseif(50 === n){
                d -= 1;
            }
        }
        return d;
    }

    qiAccurate = function(w){
        var t = self.saLonT(w) * 36525;
        return t - self.dtT(t) + self.ONE_THIRD;
    }

    qiAccurate2 = function(jd){
        var d = ..math.pi / 12;
        var w = ..math.floor((jd + 293) / 365.2422 * 24) * d;
        var a = self.qiAccurate(w);
        if(a - jd > 5){
            return self.qiAccurate(w - d);
        }
        return a - jd < -5 ? self.qiAccurate(w + d) : a;
    }
}

/**intellisense(time.tyme)
ShouXingUtil = 寿星天文历算工具类\n提供各种天文历算函数
end intellisense**/
